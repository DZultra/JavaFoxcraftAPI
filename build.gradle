plugins {
    id 'java-library'
    id 'maven-publish'
    id 'org.jreleaser' version '1.22.0'
}

def home = System.getProperty("user.home")

group = 'net.dzultra'
version = '1.0.7'

repositories {
    mavenCentral()
}

dependencies {
    testImplementation platform('org.junit:junit-bom:5.10.0')
    testImplementation 'org.junit.jupiter:junit-jupiter'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

    api 'com.google.code.gson:gson:2.10.1'
    api 'org.jsoup:jsoup:1.16.1'
    compileOnly 'org.jetbrains:annotations:24.0.1'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
    withSourcesJar()
    withJavadocJar()
}

test {
    useJUnitPlatform()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId = 'net.dzultra'
            artifactId = 'JavaFoxcraftAPI-21'
            from components.java

            pom {
                name = 'JavaFoxcraftAPI-21'
                description = 'JavaFoxcraftAPI is an API wrapper written in Java for the API of the Minecraft Server Foxcraft\'s website. (Backport to Java 21)'
                url = 'https://github.com/DZultra/JavaFoxcraftAPI'
                inceptionYear = '2021'
                licenses {
                    license {
                        name = 'MIT'
                        url = 'https://opensource.org/license/mit'
                    }
                }
                developers {
                    developer {
                        id = 'DZultra'
                        name = 'Daniel Zink'
                    }
                }
                scm {
                    connection = 'scm:git:https://github.com/DZultra/JavaFoxcraftAPI.git'
                    developerConnection = 'scm:git:ssh://github.com/DZultra/JavaFoxcraftAPI.git'
                    url = 'https://github.com/DZultra/JavaFoxcraftAPI.git'
                }
            }
        }
    }

    repositories { maven { url = layout.buildDirectory.dir('staging-deploy') } }
}

jreleaser {
    project {
        name = 'JavaFoxcraftAPI-21'           // artifactId
        group = rootProject.group.toString() // groupId
        version = rootProject.version.toString()
    }

    signing {
        pgp {
            active = 'ALWAYS'
            armored = true
            mode = 'FILE'
            publicKey = "$home/.jreleaser/public.asc"
            secretKey = "$home/.jreleaser/private.asc"
            passphrase = findProperty("passphrase")
        }
    }

    deploy {
        maven {
            enabled = true
            mavenCentral {
                sonatype {
                    active = 'RELEASE'
                    url = 'https://central.sonatype.com/api/v1/publisher'
                    stagingRepository('build/staging-deploy')
                    sign = true
                    sourceJar = true
                    javadocJar = true
                    username = findProperty("mavenUser")
                    password = findProperty("mavenPassword")
                }
                pomchecker {
                    version = rootProject.version.toString()
                }
            }
        }
    }
}